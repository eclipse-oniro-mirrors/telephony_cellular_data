# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")

SUBSYSTEM_DIR = "../.."
TELEPHONY_DATA_DIR = "../../../telephony_data"
config("cellular_data_util_config") {
  # header file path
  include_dirs = [
    "$SUBSYSTEM_DIR/interfaces/innerkits",
    "$SUBSYSTEM_DIR/services/include/apn_manager",
    "$SUBSYSTEM_DIR/services/include/common",
    "$TELEPHONY_DATA_DIR/interfaces/innerkits/include",
  ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

ohos_shared_library("tel_cellular_data_api") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
  sources = [
    "$SUBSYSTEM_DIR/frameworks/native/cellular_data_client.cpp",
    "$SUBSYSTEM_DIR/frameworks/native/cellular_data_service_proxy.cpp",
    "$SUBSYSTEM_DIR/frameworks/native/data_sim_account_call_back.cpp",
    "$SUBSYSTEM_DIR/frameworks/native/sim_account_callback_stub.cpp",
  ]

  version_script = "$SUBSYSTEM_DIR/interfaces/innerkits/libtel_cellular_data_api.versionscript"

  public_configs = [ ":cellular_data_util_config" ]

  external_deps = [
    "c_utils:utils",
    "core_service:libtel_common",
    "core_service:tel_core_service_api",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
    "telephony_data:tel_telephony_data",
  ]
  defines = [
    "TELEPHONY_LOG_TAG = \"CellularDataApi\"",
    "LOG_DOMAIN = 0xD001F03",
  ]
  innerapi_tags = [ "platformsdk" ]
  part_name = "cellular_data"
  subsystem_name = "telephony"
}
